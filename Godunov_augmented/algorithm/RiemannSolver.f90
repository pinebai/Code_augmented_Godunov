module RiemannSolver

use grid_and_parameters
! 'grid_parameter.f90'

use physics
! 'physics.f90'


contains
!******************************************************************************************


!******************************************************************************************
SUBROUTINE RIEMANN0(R1,U1,P1,R2,U2,P2,U_STAR,P_STAR,WTYPE)

implicit none

integer :: WTYPE,LWAVE,RWAVE,MWAVE,WAVETYPE
real*8 :: R1,U1,P1,R2,U2,P2
real*8 :: G1, G2, G3, G4, G5, G6, G7, G8, G9
real*8 :: DL, UL, PL, CL, DR, UR, PR, CR, PM, UM
real*8 :: U_STAR, P_STAR

COMMON /GAMMAS/G1, G2, G3, G4, G5, G6, G7, G8, G9
COMMON /STATES/ DL, UL, PL, CL, DR, UR, PR, CR, PM, UM
COMMON /WAVETYPE/ LWAVE,RWAVE,MWAVE,WAVETYPE
DL = R1
UL = U1
PL = P1
DR = R2
UR = U2
PR = P2

!	COMPUTE GAMMAS
G1 = (GAMMA-1.0D0)/(2.0D0*GAMMA)
G2 = (GAMMA+1.0D0)/(2.0D0*GAMMA)
G3 = 2.0D0*GAMMA/(GAMMA-1.0D0)
G4 = 2.0D0/(GAMMA-1.0D0)
G5 = 2.0D0/(GAMMA+1.0D0)
G6 = (GAMMA-1.0D0)/(GAMMA+1.0D0)
G7 = 0.5D0*(GAMMA-1.0D0)
G8 = 1.0D0/GAMMA
G9 = GAMMA-1.0D0

!	COMPUTE SOUND SPEEDS
CL = DSQRT(GAMMA*PL/DL)
CR = DSQRT(GAMMA*PR/DR)

!	EXACT RIEMANN SOLVER FOR PM AND UM IS CALLED
CALL COMPUTEPMUN()
WTYPE = WAVETYPE

U_STAR=UM; P_STAR=PM

END SUBROUTINE RIEMANN0
!******************************************************************************************

!******************************************************************************************
SUBROUTINE COMPUTEPMUN()
!	COMPUTE PRESSURE PM AND VELOCITY UM IN THE STAR REGION
!	NEWTON-RAPHSON METHOD USED TO FIND FM
implicit none

integer :: WTYPE,LWAVE,RWAVE,MWAVE,WAVETYPE,K
real*8 :: G1, G2, G3, G4, G5, G6, G7, G8, G9
real*8 :: DL, UL, PL, CL, DR, UR, PR, CR, PM, UM
real*8 :: SML,SMR

real*8 :: DU,DUCRIT,P0,FL,FLD,FR,FRD,CHA

COMMON /GAMMAS/ G1, G2, G3, G4, G5, G6, G7, G8, G9
COMMON /STATES/ DL, UL, PL, CL, DR, UR, PR, CR, PM, UM
COMMON /VACCUM/ SML,SMR
COMMON /WAVETYPE/ LWAVE,RWAVE,MWAVE,WAVETYPE
!DATA TOL, NRITER /1.0D-6, 200/

MWAVE = 0
DU = UR-UL

!	COMPUTE CRITICAL VELOCITY
DUCRIT = G4*(CL+CR)-DU

!	CHECK FOR GENERATION OF VACUUM
IF(DUCRIT .LE. 0.0D0) THEN
!	VACUUM IS GENERATED BY GIVEN DATA
	MWAVE = 4 !VACCUM
	!WRITE(*,*) 'VACUUM IS GENERATED BY GIVEN DATA'
	SML = UL + 2.0D0*CL/G9
	SMR = UR - 2.0D0*CR/G9
	IF(SML .GT. SML) THEN
		WRITE(*,*)'ERROR OCCURED!'
		STOP
	ENDIF
	LWAVE = 0 !R
	RWAVE = 0 !R
	WAVETYPE = LWAVE+MWAVE+RWAVE
	RETURN
ENDIF

!	GUESS VALUE IS COMPUTED
CALL STARTE()
P0=PM
DO K=1, NRITER
	CALL PREFUN(FL,FLD,PM,DL,PL,CL)
	CALL PREFUN(FR,FRD,PM,DR,PR,CR)
	PM = PM - (FL+FR+DU)/(FLD+FRD)
	CHA = 2.0D0*DABS((PM-P0)/(PM+P0))
	IF(CHA.LE.TOL) GOTO 0002
	P0 = PM
	if(pm<0) pm=max(pm,1.0d-10)
ENDDO
WRITE(*,*) 'DIVERGENCE IN NEWTON-RAPHSON SCHEME, ERROR=', CHA
0002 CONTINUE
!	COMPUTE UM
UM = 0.5D0*(UL+UR+FR-FL)

IF(PM .GT. PL)THEN
	LWAVE = 2 !S
ELSE 
	LWAVE = 0 !R
ENDIF

IF(PM .GT. PR)THEN
	RWAVE = 1 !S
ELSE 
	RWAVE = 0 !R
ENDIF
WAVETYPE = LWAVE+MWAVE+RWAVE
IF(WAVETYPE.LT.0 .OR. WAVETYPE.GT.4)THEN
	WRITE(*,*)'RIEMANN EXACT SOLVER ERROR,UNKNOWN WAVE TYPE'
	STOP
ENDIF
RETURN
END SUBROUTINE COMPUTEPMUN
!******************************************************************************************

!******************************************************************************************
!
SUBROUTINE STARTE()
!	HYBRID STARTRT USING PVRS, TRRS AND TSRS
implicit none

real*8 :: G1, G2, G3, G4, G5, G6, G7, G8, G9
real*8 :: DL, UL, PL, CL, DR, UR, PR, CR, PM, UM
real*8 :: PV, PMIN, PMAX, QRAT, PNU, PDE, GEL, GER    

COMMON /GAMMAS/ G1, G2, G3, G4, G5, G6, G7, G8, G9
COMMON /STATES/ DL, UL, PL, CL, DR, UR, PR, CR, PM, UM


!	COMPUTE GUESS VALUE FROM PVRS RIEMANN SOLVER
PV   = 0.5D0*(PL+PR)-0.125*(UR-UL)*(DL+DR)*(CL+CR)
PMIN = DMIN1(PL,PR)
PMAX = DMAX1(PL,PR)
QRAT = PMAX/PMIN
IF(QRAT.LE.QMAX.AND.(PMIN.LE.PV.AND.PV.LE.PMAX))THEN
!	USE PVRS SOLUTION AS GUESS
	PM = DMAX1(TOL,PV)
ELSE
	IF(PV.LT.PMIN)THEN
		!	USE TWO-RAREFACTION SOLUTION
		PNU = CL+CR-G7*(UR-UL)
		PDE = CL/PL**G1 + CR/PR**G1
		PM   = (PNU/PDE)**G3
	ELSE
		!	TWO-SHOCK APPROXIMATION WITH PV AS ESTIMATE
		GEL = DSQRT((G5/DL)/(G6*PL+DMAX1(TOL,PV)))
		GER = DSQRT((G5/DR)/(G6*PR+DMAX1(TOL,PV)))
		PM   = (GEL*PL+GER*PR-(UR-UL))/(GEL+GER)
		PM   = DMAX1(TOL,PM)
	ENDIF
ENDIF
RETURN
END SUBROUTINE STARTE
!******************************************************************************************

!******************************************************************************************
SUBROUTINE PREFUN(F,FD,P,DK,PK,CK)
!	PRESSURE FUNCTIONS ARE EVALUATED
implicit none

real*8 :: G1, G2, G3, G4, G5, G6, G7, G8, G9
real*8 :: F,FD,P,DK,PK,CK
real*8 :: AK,BK,QRT,PRAT
COMMON /GAMMAS/ G1, G2, G3, G4, G5, G6, G7, G8, G9
IF(P.LE.PK)THEN
	!	RAREFACTION WAVE
	PRAT = P/PK
	F    = G4*CK*(PRAT**G1-1.0D0)
	FD   = (1.0D0/(DK*CK))*PRAT**(-G2)
ELSE
	!	SHOCK WAVE
	AK   = G5/DK
	BK   = G6*PK
	QRT  = DSQRT(AK/(BK+P))
	F    = (P-PK)*QRT
	FD   = (1.0D0-0.5D0*(P-PK)/(BK+P))*QRT
ENDIF
RETURN
END SUBROUTINE PREFUN
!********************************************************************************************


SUBROUTINE RIEMANN1(S,D,U,P)
implicit none

integer :: WTYPE,LWAVE,RWAVE,MWAVE,WAVETYPE
real*8 :: G1, G2, G3, G4, G5, G6, G7, G8, G9
real*8 :: DL, UL, PL, CL, DR, UR, PR, CR, PM, UM
real*8 :: SML,SMR
real*8 :: S,D,U,P
real*8 :: C,SHL,CML,STL,PML,SL,PMR,SR,SHR,CMR,STR

COMMON /GAMMAS/ G1, G2, G3, G4, G5, G6, G7, G8, G9
COMMON /STATES/ DL, UL, PL, CL, DR, UR, PR, CR, PM, UM
COMMON /VACCUM/ SML,SMR
COMMON /WAVETYPE/ LWAVE,RWAVE,MWAVE,WAVETYPE

IF(WAVETYPE .EQ. 4) THEN
! IF VACCUM CREATED
	IF(S.LE.UL-CL)THEN
		D = DL
		U = UL
		P = PL
	ELSE IF(S.LT.SML)THEN
		U = G5*(CL+G7*UL+S)
		C = G5*(CL+G7*(UL-S))
		D = DL*(C/CL)**G4
		P = PL*(C/CL)**G3
	ELSE IF(S.LE.SMR)THEN
		D = 0.0D0
		U = (SML+SMR)/2 !0.0D0
		P = 0.0D0
	ELSE IF(S.LT.UR+CR)THEN
		U = G5*(-CR+G7*UR+S)
		C = G5*(CR-G7*(UR-S))
		D = DR*(C/CR)**G4
		P = PR*(C/CR)**G3
	ELSE
		D = DR
		U = UR
		P = PR
	ENDIF
ELSE
! NO VACCUM CREATED
	IF(S.LE.UM)THEN
		!	SAMPLE POINT IS TO THE LEFT OF THE CONTACT
		IF(PM.LE.PL)THEN
			!	LEFT FAN
			SHL = UL-CL
			IF(S.LE.SHL)THEN
				!	LEFT DATA STATE
				D = DL
				U = UL
				P = PL
			ELSE
				CML = CL*(PM/PL)**G1
				STL = UM-CML
				IF(S.GT.STL)THEN
					!	MIDDLE LEFT STATE
					D = DL*(PM/PL)**G8
					U = UM
					P = PM
				ELSE
					!	AN LEFT STATE (INSIDE FAN)
					U = G5*(CL+G7*UL+S)
					C = G5*(CL+G7*(UL-S))
					D = DL*(C/CL)**G4
					P = PL*(C/CL)**G3
				ENDIF
			ENDIF
		ELSE
			!	LEFT SHOCK
			PML = PM/PL
			SL  = UL-CL*DSQRT(G2*PML+G1)
			IF(S.LE.SL)THEN
				!	LEFT DATA STATE
				D = DL
				U = UL
				P = PL
			ELSE
				!	MODDLE LEFT STATE (BEHIND SHOCK)
				D = DL*(PML+G6)/(PML*G6+1.0D0)
				U = UM
				P = PM
			ENDIF
		ENDIF
	ELSE
		!	RIGHT OF CONTACT
		IF(PM.GT.PR)THEN
			!	RIGHT SHOCK
			PMR = PM/PR
			SR  = UR+CR*DSQRT(G2*PMR+G1)
			IF(S.GE.SR)THEN
				!	RIGHT DATA STATE
				D = DR
				U = UR
				P = PR
			ELSE
				!	MIDDLE RIGHT STATE (BEHIND SHOCK)
				D = DR*(PMR+G6)/(PMR*G6+1.0D0)
				U = UM
				P = PM
			ENDIF
		ELSE
			!	RIGHT FAN
			SHR = UR+CR
			IF(S.GE.SHR)THEN
				!	RIGHT DATA STATE
				D = DR
				U = UR
				P = PR
			ELSE
				CMR = CR*(PM/PR)**G1
				STR = UM+CMR
				IF(S.LE.STR)THEN
					!	MIDDLE RIGHT STATE
					D = DR*(PM/PR)**G8
					U = UM
					P = PM
				ELSE
					!	FAN RIGHT STATE (INSIDE FAN)
					U = G5*(-CR+G7*UR+S)
					C = G5*(CR-G7*(UR-S))
					D = DR*(C/CR)**G4
					P = PR*(C/CR)**G3
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

RETURN

END SUBROUTINE RIEMANN1
!******************************************************************************************


end module RiemannSolver
!******************************************************************************************